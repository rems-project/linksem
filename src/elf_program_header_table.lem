(** [elf_program_header_table] contains type, functions and other definitions
  * for working with program header tables and their entries and ELF segments.
  * Related files are [elf_interpreted_segments] which extracts information
  * derived from PHTs presented in this file and converts it into a more usable
  * format for processing.
  *
  * FIXME:
  * Bug in Lem as Lem codebase uses [int] type throughout where [BigInt.t]
  * is really needed, hence chokes on huge constants below, which is why they are
  * written in the way that they are.
  *)

open import Assert_extra
open import Basic_classes
open import Bool
open import Function
open import List
open import Maybe
open import Num
open import String
import Set

open import Elf_types_native_uint
open import Endianness

open import Byte_sequence
open import Error
open import Missing_pervasives
open import Show

(** Segment types *)

(** Unused array element.  All other members of the structure are undefined. *)
let elf_pt_null : natural = 0
(** A loadable segment. *)
let elf_pt_load : natural = 1
(** Dynamic linking information. *)
let elf_pt_dynamic : natural = 2
(** Specifies the location and size of a null-terminated path name to be used to
  * invoke an interpreter.
  *)
let elf_pt_interp : natural = 3
(** Specifies location and size of auxiliary information. *)
let elf_pt_note : natural = 4
(** Reserved but with unspecified semantics.  If the file contains a segment of
  * this type then it is to be regarded as non-conformant with the ABI.
  *)
let elf_pt_shlib : natural = 5
(** Specifies the location and size of the program header table. *)
let elf_pt_phdr : natural = 6
(** Specifies the thread local storage (TLS) template.  Need not be supported. *)
let elf_pt_tls : natural = 7
(** Start of reserved indices for operating system specific semantics. *)
let elf_pt_loos : natural = 128 * 128 * 128 * 256 * 3 (* 1610612736 (* 0x60000000 *) *)
(** End of reserved indices for operating system specific semantics. *)
let elf_pt_hios : natural = (469762047 * 4) + 3 (* 1879048191 (* 0x6fffffff *) *)
(** Start of reserved indices for processor specific semantics. *)
let elf_pt_loproc : natural = (469762048 * 4) (* 1879048192 (* 0x70000000 *) *)
(** End of reserved indices for processor specific semantics. *)
let elf_pt_hiproc : natural = (536870911 * 4) + 3 (* 2147483647 (* 0x7fffffff *) *)

(** [string_of_elf_segment_type os proc st] produces a string representation of
  * the coding of an ELF segment type [st] using [os] and [proc] to render OS-
  * and processor-specific codings.
  *)
(* XXX: is GNU stuff supposed to be hardcoded here? *)
val string_of_segment_type : (natural -> string) -> (natural -> string) -> natural -> string
let string_of_segment_type os proc pt =
	if pt = elf_pt_null then
		"NULL"
	else if pt = elf_pt_load then
		"LOAD"
	else if pt = elf_pt_dynamic then
		"DYNAMIC"
	else if pt = elf_pt_interp then
		"INTERP"
	else if pt = elf_pt_note then
		"NOTE"
	else if pt = elf_pt_shlib then
		"SHLIB"
	else if pt = elf_pt_phdr then
		"PHDR"
	else if pt = elf_pt_tls then
		"TLS"
	else if pt >= elf_pt_loos && pt <= elf_pt_hios then
		os pt
	else if pt >= elf_pt_loproc && pt <= elf_pt_hiproc then
		proc pt
	else
		"Undefined or invalid segment type"

(** Segments permission flags *)

(** Execute bit *)
let elf_pf_x        : natural = 1
(** Write bit *)
let elf_pf_w        : natural = 2
(** Read bit *)
let elf_pf_r        : natural = 4
(** The following two bit ranges are reserved for OS- and processor-specific
  * flags respectively.
  *)
let elf_pf_maskos   : natural = 267386880      (* 0x0ff00000 *)
let elf_pf_maskproc : natural = 4 * 1006632960 (* 0xf0000000 *)

(** [exact_permission_of_permission m]: ELF has two interpretations of a RWX-style
  * permission bit [m], an exact permission and an allowable permission.  These
  * permissions allow us to interpret a flag as an upper bound for behaviour and
  * an ABI-compliant implementation can choose to interpret the flag [m] as either.
  *
  * In the exact interpretation, the upper bound is exactly the natural interpretation
  * of the flag.  This is encoded in [exact_permission_of_permission], which is
  * a glorified identity function, though included for completeness.
  *)
val exact_permissions_of_permission : natural -> error natural
let exact_permissions_of_permission m =
  if m = 0 then
    return 0
  else if m = elf_pf_x then
    return 1
  else if m = elf_pf_w then
    return 2
  else if m = elf_pf_r then
    return 4
  else if m = elf_pf_x + elf_pf_w then
    return 3
  else if m = elf_pf_x + elf_pf_r then
    return 5
  else if m = elf_pf_w + elf_pf_r then
    return 6
  else if m = elf_pf_x + elf_pf_r + elf_pf_w then
    return 7
  else
    fail "exact_permission_of_permission: invalid permission flag"

(** [allowable_permission_of_permission m]: ELF has two interpretations of a RWX-style
  * permission bit [m], an exact permission and an allowable permission.  These
  * permissions allow us to interpret a flag as an upper bound for behaviour and
  * an ABI-compliant implementation can choose to interpret the flag [m] as either.
  *
  * In the allowable interpretation, the upper bound is more lax than the natural
  * interpretation of the flag.
  *)
val allowable_permissions_of_permission : natural -> error natural
let allowable_permissions_of_permission m =
  if m = 0 then
    return 0
  else if m = elf_pf_x then
    return 5
  else if m = elf_pf_w then
    return 7
  else if m = elf_pf_r then
    return 5
  else if m = elf_pf_x + elf_pf_w then
    return 7
  else if m = elf_pf_x + elf_pf_r then
    return 5
  else if m = elf_pf_w + elf_pf_r then
    return 7
  else if m = elf_pf_x + elf_pf_r + elf_pf_w then
    return 7
  else
    fail "exact_permission_of_permission: invalid permission flag"

(** [elf64_interpreted_program_header_flags w] extracts a boolean triple of flags
  * from the flags field of an interpreted segment.
  *)
val parse_elf_segment_permissions : natural -> (bool * bool * bool)
let parse_elf_segment_permissions m =
  if m = 0 then
    (false, false, false)
  else if m = elf_pf_x then
    (false, false, true)
  else if m = elf_pf_w then
    (false, true, false)
  else if m = elf_pf_r then
    (true, false, false)
  else if m = elf_pf_x + elf_pf_w then
    (false, true, true)
  else if m = elf_pf_x + elf_pf_r then
    (true, false, true)
  else if m = elf_pf_w + elf_pf_r then
    (true, true, false)
  else if m = elf_pf_x + elf_pf_r + elf_pf_w then
    (true, true, true)
  else
    failwith "Invalid permisssion flag"

(** [string_of_elf_segment_permissions m] produces a string-based representation
  * of an ELF segment's permission field.
  * TODO: expand this as is needed by the validation tests.
  *)
val string_of_elf_segment_permissions : natural -> string
let string_of_elf_segment_permissions m =
  let (r, w, x) = parse_elf_segment_permissions m in
  (if r then "R" else " ") ^ (if w then "W" else " ") ^ (if x then "X" else " ")

(** Program header table entry type *)

(** Type [elf32_program_header_table_entry] encodes a program header table entry
  * for 32-bit platforms.  Each entry describes a segment in an executable or
  * shared object file.
  *)
type elf32_program_header_table_entry =
  <| elf32_p_type   : elf32_word (** Type of the segment *)
   ; elf32_p_offset : elf32_off  (** Offset from beginning of file for segment *)
   ; elf32_p_vaddr  : elf32_addr (** Virtual address for segment in memory *)
   ; elf32_p_paddr  : elf32_addr (** Physical address for segment *)
   ; elf32_p_filesz : elf32_word (** Size of segment in file, in bytes *)
   ; elf32_p_memsz  : elf32_word (** Size of segment in memory image, in bytes *)
   ; elf32_p_flags  : elf32_word (** Segment flags *)
   ; elf32_p_align  : elf32_word (** Segment alignment memory for memory and file *)
   |>

(** [compare_elf32_program_header_table_entry ent1 ent2] is an ordering-comparison
  * function on program header table entries suitable for constructing sets,
  * finite maps, and other ordered data types with.
  *)
val compare_elf32_program_header_table_entry : elf32_program_header_table_entry ->
  elf32_program_header_table_entry -> ordering
let compare_elf32_program_header_table_entry h1 h2 =
    compare [natural_of_elf32_word h1.elf32_p_type;
    natural_of_elf32_off h1.elf32_p_offset;
    natural_of_elf32_addr h1.elf32_p_vaddr;
    natural_of_elf32_addr h1.elf32_p_paddr;
    natural_of_elf32_word h1.elf32_p_filesz;
    natural_of_elf32_word h1.elf32_p_memsz;
    natural_of_elf32_word h1.elf32_p_flags;
    natural_of_elf32_word h1.elf32_p_align]
    [natural_of_elf32_word h2.elf32_p_type;
    natural_of_elf32_off h2.elf32_p_offset;
    natural_of_elf32_addr h2.elf32_p_vaddr;
    natural_of_elf32_addr h2.elf32_p_paddr;
    natural_of_elf32_word h2.elf32_p_filesz;
    natural_of_elf32_word h2.elf32_p_memsz;
    natural_of_elf32_word h2.elf32_p_flags;
    natural_of_elf32_word h2.elf32_p_align]

instance (Ord elf32_program_header_table_entry)
    let compare = compare_elf32_program_header_table_entry
    let (<) = fun f1 -> (fun f2 -> (compare_elf32_program_header_table_entry f1 f2 = LT))
    let (<=) = fun f1 -> (fun f2 -> Set.member (compare_elf32_program_header_table_entry f1 f2) {LT; EQ})
    let (>) = fun f1 -> (fun f2 -> (compare_elf32_program_header_table_entry f1 f2 = GT))
    let (>=) = fun f1 -> (fun f2 -> Set.member (compare_elf32_program_header_table_entry f1 f2) {GT; EQ})
end

(** Type [elf64_program_header_table_entry] encodes a program header table entry
  * for 64-bit platforms.  Each entry describes a segment in an executable or
  * shared object file.
  *)
type elf64_program_header_table_entry =
  <| elf64_p_type   : elf64_word  (** Type of the segment *)
   ; elf64_p_flags  : elf64_word  (** Segment flags *)
   ; elf64_p_offset : elf64_off   (** Offset from beginning of file for segment *)
   ; elf64_p_vaddr  : elf64_addr  (** Virtual address for segment in memory *)
   ; elf64_p_paddr  : elf64_addr  (** Physical address for segment *)
   ; elf64_p_filesz : elf64_xword (** Size of segment in file, in bytes *)
   ; elf64_p_memsz  : elf64_xword (** Size of segment in memory image, in bytes *)
   ; elf64_p_align  : elf64_xword (** Segment alignment memory for memory and file *)
   |>

(** [compare_elf64_program_header_table_entry ent1 ent2] is an ordering-comparison
  * function on program header table entries suitable for constructing sets,
  * finite maps, and other ordered data types with.
  *)
val compare_elf64_program_header_table_entry : elf64_program_header_table_entry ->
  elf64_program_header_table_entry -> ordering
let compare_elf64_program_header_table_entry h1 h2 =
    compare [natural_of_elf64_word h1.elf64_p_type;
    natural_of_elf64_off h1.elf64_p_offset;
    natural_of_elf64_addr h1.elf64_p_vaddr;
    natural_of_elf64_addr h1.elf64_p_paddr;
    natural_of_elf64_xword h1.elf64_p_filesz;
    natural_of_elf64_xword h1.elf64_p_memsz;
    natural_of_elf64_word h1.elf64_p_flags;
    natural_of_elf64_xword h1.elf64_p_align]
    [natural_of_elf64_word h2.elf64_p_type;
    natural_of_elf64_off h2.elf64_p_offset;
    natural_of_elf64_addr h2.elf64_p_vaddr;
    natural_of_elf64_addr h2.elf64_p_paddr;
    natural_of_elf64_xword h2.elf64_p_filesz;
    natural_of_elf64_xword h2.elf64_p_memsz;
    natural_of_elf64_word h2.elf64_p_flags;
    natural_of_elf64_xword h2.elf64_p_align]

instance (Ord elf64_program_header_table_entry)
    let compare = compare_elf64_program_header_table_entry
    let (<) = fun f1 -> (fun f2 -> (compare_elf64_program_header_table_entry f1 f2 = LT))
    let (<=) = fun f1 -> (fun f2 -> Set.member (compare_elf64_program_header_table_entry f1 f2) {LT; EQ})
    let (>) = fun f1 -> (fun f2 -> (compare_elf64_program_header_table_entry f1 f2 = GT))
    let (>=) = fun f1 -> (fun f2 -> Set.member (compare_elf64_program_header_table_entry f1 f2) {GT; EQ})
end


(** [string_of_elf32_program_header_table_entry os proc et] produces a string
  * representation of a 32-bit program header table entry using [os] and [proc]
  * to render OS- and processor-specific entries.
  *)
val string_of_elf32_program_header_table_entry : (natural -> string) -> (natural -> string) -> elf32_program_header_table_entry -> string
let string_of_elf32_program_header_table_entry os proc entry =
	unlines [
		"\t" ^ "Segment type: " ^ string_of_segment_type os proc (natural_of_elf32_word entry.elf32_p_type)
	; "\t" ^ "Offset: " ^ show entry.elf32_p_offset
	; "\t" ^ "Virtual address: " ^ show entry.elf32_p_vaddr
	; "\t" ^ "Physical address: " ^ show entry.elf32_p_paddr
	; "\t" ^ "Segment size (bytes): " ^ show entry.elf32_p_filesz
	; "\t" ^ "Segment size in memory image (bytes): " ^ show entry.elf32_p_memsz
	; "\t" ^ "Flags: " ^ show entry.elf32_p_flags
  ; "\t" ^ "Alignment: " ^ show entry.elf32_p_align
	]

(** [string_of_elf64_program_header_table_entry os proc et] produces a string
  * representation of a 64-bit program header table entry using [os] and [proc]
  * to render OS- and processor-specific entries.
  *)
val string_of_elf64_program_header_table_entry : (natural -> string) -> (natural -> string) -> elf64_program_header_table_entry -> string
let string_of_elf64_program_header_table_entry os proc entry =
  unlines [
    "\t" ^ "Segment type: " ^ string_of_segment_type os proc (natural_of_elf64_word entry.elf64_p_type)
  ; "\t" ^ "Offset: " ^ show entry.elf64_p_offset
  ; "\t" ^ "Virtual address: " ^ show entry.elf64_p_vaddr
  ; "\t" ^ "Physical address: " ^ show entry.elf64_p_paddr
  ; "\t" ^ "Segment size (bytes): " ^ show entry.elf64_p_filesz
  ; "\t" ^ "Segment size in memory image (bytes): " ^ show entry.elf64_p_memsz
  ; "\t" ^ "Flags: " ^ show entry.elf64_p_flags
  ; "\t" ^ "Alignment: " ^ show entry.elf64_p_align
  ]

(** [string_of_elf32_program_header_table_entry_default et] produces a string representation
  * of table entry [et] where OS- and processor-specific entries are replaced with
  * default strings.
  *)
val string_of_elf32_program_header_table_entry_default : elf32_program_header_table_entry -> string
let string_of_elf32_program_header_table_entry_default =
	string_of_elf32_program_header_table_entry
    (const "*Default OS specific print*")
      (const "*Default processor specific print*")

(** [string_of_elf64_program_header_table_entry_default et] produces a string representation
  * of table entry [et] where OS- and processor-specific entries are replaced with
  * default strings.
  *)
val string_of_elf64_program_header_table_entry_default : elf64_program_header_table_entry -> string
let string_of_elf64_program_header_table_entry_default =
  string_of_elf64_program_header_table_entry
    (const "*Default OS specific print*")
      (const "*Default processor specific print*")

instance (Show elf32_program_header_table_entry)
	let show = string_of_elf32_program_header_table_entry_default
end

instance (Show elf64_program_header_table_entry)
  let show = string_of_elf64_program_header_table_entry_default
end

(** Parsing and blitting *)

(** [bytes_of_elf32_program_header_table_entry ed ent] blits a 32-bit program
  * header table entry [ent] into a byte sequence assuming endianness [ed].
  *)
val bytes_of_elf32_program_header_table_entry : endianness -> elf32_program_header_table_entry -> byte_sequence
let bytes_of_elf32_program_header_table_entry endian entry =
  Byte_sequence.from_byte_lists [
    bytes_of_elf32_word endian entry.elf32_p_type
  ; bytes_of_elf32_off  endian entry.elf32_p_offset
  ; bytes_of_elf32_addr endian entry.elf32_p_vaddr
  ; bytes_of_elf32_addr endian entry.elf32_p_paddr
  ; bytes_of_elf32_word endian entry.elf32_p_filesz
  ; bytes_of_elf32_word endian entry.elf32_p_memsz
  ; bytes_of_elf32_word endian entry.elf32_p_flags
  ; bytes_of_elf32_word endian entry.elf32_p_align
  ]

(** [bytes_of_elf64_program_header_table_entry ed ent] blits a 64-bit program
  * header table entry [ent] into a byte sequence assuming endianness [ed].
  *)
val bytes_of_elf64_program_header_table_entry : endianness -> elf64_program_header_table_entry -> byte_sequence
let bytes_of_elf64_program_header_table_entry endian entry =
  Byte_sequence.from_byte_lists [
    bytes_of_elf64_word  endian entry.elf64_p_type
  ; bytes_of_elf64_word  endian entry.elf64_p_flags
  ; bytes_of_elf64_off   endian entry.elf64_p_offset
  ; bytes_of_elf64_addr  endian entry.elf64_p_vaddr
  ; bytes_of_elf64_addr  endian entry.elf64_p_paddr
  ; bytes_of_elf64_xword endian entry.elf64_p_filesz
  ; bytes_of_elf64_xword endian entry.elf64_p_memsz
  ; bytes_of_elf64_xword endian entry.elf64_p_align
  ]

(** [read_elf32_program_header_table_entry endian bs0] reads an ELF32 program header table
  * entry from byte sequence [bs0] assuming endianness [endian].  If [bs0] is larger
  * than necessary, the excess is returned from the function, too.
  * Fails if the entry cannot be read.
  *)
val read_elf32_program_header_table_entry : endianness -> byte_sequence ->
  error (elf32_program_header_table_entry * byte_sequence)
let read_elf32_program_header_table_entry endian bs =
	read_elf32_word endian bs >>= fun (typ, bs) ->
	read_elf32_off  endian bs >>= fun (offset, bs) ->
	read_elf32_addr endian bs >>= fun (vaddr, bs) ->
	read_elf32_addr endian bs >>= fun (paddr, bs) ->
	read_elf32_word endian bs >>= fun (filesz, bs) ->
	read_elf32_word endian bs >>= fun (memsz, bs) ->
	read_elf32_word endian bs >>= fun (flags, bs) ->
	read_elf32_word endian bs >>= fun (align, bs) ->
		return (<| elf32_p_type = typ; elf32_p_offset = offset;
                elf32_p_vaddr = vaddr; elf32_p_paddr = paddr;
                elf32_p_filesz = filesz; elf32_p_memsz = memsz;
                elf32_p_flags = flags; elf32_p_align = align |>, bs)

(** [read_elf64_program_header_table_entry endian bs0] reads an ELF64 program header table
  * entry from byte sequence [bs0] assuming endianness [endian].  If [bs0] is larger
  * than necessary, the excess is returned from the function, too.
  * Fails if the entry cannot be read.
  *)
val read_elf64_program_header_table_entry : endianness -> byte_sequence ->
  error (elf64_program_header_table_entry * byte_sequence)
let read_elf64_program_header_table_entry endian bs =
  read_elf64_word endian bs >>= fun (typ, bs) ->
  read_elf64_word endian bs >>= fun (flags, bs) ->
  read_elf64_off  endian bs >>= fun (offset, bs) ->
  read_elf64_addr endian bs >>= fun (vaddr, bs) ->
  read_elf64_addr endian bs >>= fun (paddr, bs) ->
  read_elf64_xword endian bs >>= fun (filesz, bs) ->
  read_elf64_xword endian bs >>= fun (memsz, bs) ->
  read_elf64_xword endian bs >>= fun (align, bs) ->
    return (<| elf64_p_type = typ; elf64_p_offset = offset;
                elf64_p_vaddr = vaddr; elf64_p_paddr = paddr;
                elf64_p_filesz = filesz; elf64_p_memsz = memsz;
                elf64_p_flags = flags; elf64_p_align = align |>, bs)

(** Program header table type *)

(** Type [elf32_program_header_table] represents a program header table for 32-bit
  * ELF files.  A program header table is an array (implemented as a list, here)
  * of program header table entries.
  *)
type elf32_program_header_table =
  list elf32_program_header_table_entry

(** Type [elf64_program_header_table] represents a program header table for 64-bit
  * ELF files.  A program header table is an array (implemented as a list, here)
  * of program header table entries.
  *)
type elf64_program_header_table =
  list elf64_program_header_table_entry

(** [bytes_of_elf32_program_header_table ed tbl] blits an ELF32 program header
  * table into a byte sequence assuming endianness [ed].
  *)
let bytes_of_elf32_program_header_table endian tbl =
  Byte_sequence.concat (List.map (bytes_of_elf32_program_header_table_entry endian) tbl)

(** [bytes_of_elf64_program_header_table ed tbl] blits an ELF64 program header
  * table into a byte sequence assuming endianness [ed].
  *)
let bytes_of_elf64_program_header_table endian tbl =
  Byte_sequence.concat (List.map (bytes_of_elf64_program_header_table_entry endian) tbl)

(** [read_elf32_program_header_table' endian bs0] reads an ELF32 program header table from
  * byte_sequence [bs0] assuming endianness [endian].  The byte_sequence [bs0] is assumed
  * to have exactly the correct size for the table.  For internal use, only.  Use
  * [read_elf32_program_header_table] below instead.
  *)
let rec read_elf32_program_header_table' endian bs0 =
	if Byte_sequence.length bs0 = 0 then
  	return []
  else
  	read_elf32_program_header_table_entry endian bs0 >>= fun (entry, bs1) ->
    read_elf32_program_header_table' endian bs1 >>= fun tail ->
    return (entry::tail)

(** [read_elf64_program_header_table' endian bs0] reads an ELF64 program header table from
  * byte_sequence [bs0] assuming endianness [endian].  The byte_sequence [bs0] is assumed
  * to have exactly the correct size for the table.  For internal use, only.  Use
  * [read_elf32_program_header_table] below instead.
  *)
let rec read_elf64_program_header_table' endian bs0 =
  if Byte_sequence.length bs0 = 0 then
    return []
  else
    read_elf64_program_header_table_entry endian bs0 >>= fun (entry, bs1) ->
    read_elf64_program_header_table' endian bs1 >>= fun tail ->
    return (entry::tail)

(** [read_elf32_program_header_table table_size endian bs0] reads an ELF32 program header
  * table from byte_sequence [bs0] assuming endianness [endian] based on the size (in bytes) passed in via [table_size].
  * This [table_size] argument should be equal to the number of entries in the
  * table multiplied by the fixed entry size.  Bitstring [bs0] may be larger than
  * necessary, in which case the excess is returned.
  *)
val read_elf32_program_header_table : natural -> endianness -> byte_sequence ->
  error (elf32_program_header_table * byte_sequence)
let read_elf32_program_header_table table_size endian bs0 =
	partition table_size bs0 >>= fun (eat, rest) ->
	read_elf32_program_header_table' endian eat >>= fun table ->
	return (table, rest)

(** [read_elf64_program_header_table table_size endian bs0] reads an ELF64 program header
  * table from byte_sequence [bs0] assuming endianness [endian] based on the size (in bytes) passed in via [table_size].
  * This [table_size] argument should be equal to the number of entries in the
  * table multiplied by the fixed entry size.  Bitstring [bs0] may be larger than
  * necessary, in which case the excess is returned.
  *)
val read_elf64_program_header_table : natural -> endianness -> byte_sequence ->
  error (elf64_program_header_table * byte_sequence)
let read_elf64_program_header_table table_size endian bs0 =
  partition table_size bs0 >>= fun (eat, rest) ->
  read_elf64_program_header_table' endian eat >>= fun table ->
  return (table, rest)

(** The [pht_print_bundle] type is used to tidy up other type signatures.  Some of the
  * top-level string_of_ functions require six or more functions passed to them,
  * which quickly gets out of hand.  This type is used to reduce that complexity.
  * The first component of the type is an OS specific print function, the second is
  * a processor specific print function.
  *)
type pht_print_bundle = (natural -> string) * (natural -> string)

(** [string_of_elf32_program_header_table os proc tbl] produces a string representation
  * of program header table [tbl] using [os] and [proc] to render OS- and processor-
  * specific entries.
  *)
val string_of_elf32_program_header_table : pht_print_bundle -> elf32_program_header_table -> string
let string_of_elf32_program_header_table (os, proc) tbl =
  unlines (List.map (string_of_elf32_program_header_table_entry os proc) tbl)

(** [string_of_elf64_program_header_table os proc tbl] produces a string representation
  * of program header table [tbl] using [os] and [proc] to render OS- and processor-
  * specific entries.
  *)
val string_of_elf64_program_header_table : pht_print_bundle -> elf64_program_header_table -> string
let string_of_elf64_program_header_table (os, proc) tbl =
  unlines (List.map (string_of_elf64_program_header_table_entry os proc) tbl)

(** Static/dynamic linkage *)

(** [get_elf32_dynamic_linked pht] tests whether an ELF32 file is a dynamically
  * linked object by traversing the program header table and attempting to find
  * a header describing a segment with the name of an associated interpreter.
  * Returns [true] if any such header is found, [false] --- indicating static
  * linkage --- otherwise.
  *)
val get_elf32_dynamic_linked : elf32_program_header_table -> bool
let get_elf32_dynamic_linked pht =
  List.any (fun p -> natural_of_elf32_word p.elf32_p_type = elf_pt_interp) pht

(** [get_elf64_dynamic_linked pht] tests whether an ELF64 file is a dynamically
  * linked object by traversing the program header table and attempting to find
  * a header describing a segment with the name of an associated interpreter.
  * Returns [true] if any such header is found, [false] --- indicating static
  * linkage --- otherwise.
  *)
val get_elf64_dynamic_linked : elf64_program_header_table -> bool
let get_elf64_dynamic_linked pht =
  List.any (fun p -> natural_of_elf64_word p.elf64_p_type = elf_pt_interp) pht

(** [get_elf32_static_linked] is a utility function defined as the inverse
  * of [get_elf32_dynamic_linked].
  *)
val get_elf32_static_linked : elf32_program_header_table -> bool
let get_elf32_static_linked pht =
  not (get_elf32_dynamic_linked pht)

(** [get_elf64_static_linked] is a utility function defined as the inverse
  * of [get_elf64_dynamic_linked].
  *)
val get_elf64_static_linked : elf64_program_header_table -> bool
let get_elf64_static_linked pht =
  not (get_elf64_dynamic_linked pht)

(** [get_elf32_requested_interpreter ent bs0] extracts the requested interpreter
  * of a dynamically linkable ELF file from that file's program header table
  * entry of type PT_INTERP, [ent].  Interpreter string is extracted from byte
  * sequence [bs0].
  * Fails if [ent] is not of type PT_INTERP, or if transcription otherwise fails.
  *)
val get_elf32_requested_interpreter : elf32_program_header_table_entry ->
  byte_sequence -> error string
let get_elf32_requested_interpreter pent bs0 =
  if natural_of_elf32_word pent.elf32_p_type = elf_pt_interp then
    let off = natural_of_elf32_off  pent.elf32_p_offset in
    let siz = natural_of_elf32_word pent.elf32_p_filesz in
      Byte_sequence.offset_and_cut off (siz - 1) bs0 >>= fun cut ->
      return (Byte_sequence.string_of_byte_sequence cut)
  else
    fail "get_elf32_requested_interpreter: not an INTERP segment header"

(** [get_elf64_requested_interpreter ent bs0] extracts the requested interpreter
  * of a dynamically linkable ELF file from that file's program header table
  * entry of type PT_INTERP, [ent].  Interpreter string is extracted from byte
  * sequence [bs0].
  * Fails if [ent] is not of type PT_INTERP, or if transcription otherwise fails.
  *)
val get_elf64_requested_interpreter : elf64_program_header_table_entry ->
  byte_sequence -> error string
let get_elf64_requested_interpreter pent bs0 =
  if natural_of_elf64_word pent.elf64_p_type = elf_pt_interp then
    let off = natural_of_elf64_off   pent.elf64_p_offset in
    let siz = natural_of_elf64_xword pent.elf64_p_filesz in
      Byte_sequence.offset_and_cut off (siz - 1) bs0 >>= fun cut ->
      return (Byte_sequence.string_of_byte_sequence cut)
  else
    fail "get_elf64_requested_interpreter: not an INTERP segment header"
